plugins {
    id "nebula.kotlin" version "$kotlinVersion" apply false
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion" apply false
    id "org.jetbrains.dokka" version "0.10.1" apply false
    id("io.spinnaker.plugin.bundler").version("$spinnakerGradleVersion")
    id("com.palantir.git-version").version("0.12.2")
}

spinnakerBundle {
    pluginId = "odusanya18.drone-ci"
    description = "A Drone CI build stage plugin."
    provider = "https://github.com/Odusanya18"
    version = rootProject.version
}

version = normalizedVersion()

subprojects {
    group = "com.github.odusanya18"
    version = rootProject.version
}

String normalizedVersion() {
    String fullVersion = gitVersion()
    String normalized = fullVersion.split("-").first()
    if (fullVersion.contains("dirty")) {
        return "$normalized-SNAPSHOT"
    } else {
        return normalized
    }
}